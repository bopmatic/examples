plugins {
    id 'application'
    id 'com.google.protobuf' version '0.8.19'
    id 'idea'
}

repositories {
    maven {
        url "https://maven-central.storage-download.googleapis.com/maven2/" }
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = '1.48.0'
def protobufVersion = '3.19.2'
def protocVersion = protobufVersion
def daprJavaSDKVersion = '1.6.0'
def mockitoVersion = '4.8.0'

dependencies {
    implementation "io.dapr:dapr-sdk:${daprJavaSDKVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task myOrderServiceServer(type: CreateStartScripts) {
    mainClass = 'com.bopmatic.orders.MyOrderServiceServer'
    applicationName = 'order_server'
    outputDir = new File(project.buildDir, 'tmp/scripts/' + name)
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(myOrderServiceServer)
    fileMode = 0755
}

test {
  testLogging.showStandardStreams = true
}
